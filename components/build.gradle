apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
}

def ARTIFACT_VERSION="$VERSION_MAJOR.$VERSION_MINOR.$VERSION_PATCH"

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.3'
    compile 'org.apache.commons:commons-lang3:3.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.btmatthews.hamcrest:hamcrest-matchers:1.0.1'
}

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

archivesBaseName = 'ctct-components'

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        SdkComponent(MavenPublication) {
            from components.java
            groupId 'com.constantcontact'
            artifactId 'java-components'
            artifact sourceJar
            artifact javadocJar
            version ARTIFACT_VERSION
        }
    }
}

bintray {
    user = project.hasProperty("bintray_user") ? bintray_user : "user"
    key = project.hasProperty("bintray_key") ? bintray_key : "key"

    publications = ['SdkComponent']

    pkg {
        repo = 'maven'
        name = 'java-components'
        userOrg = 'constantcontact'
        licenses = ['BSD 3-Clause']
        websiteUrl = 'https://github.com/constantcontact/java-sdk'
        issueTrackerUrl = 'https://github.com/constantcontact/java-sdk/issues'
        vcsUrl = 'https://github.com/constantcontact/java-sdk.git'
        labels = ['constant contact', 'constant', 'contact']

        version {
            name = ARTIFACT_VERSION
            desc = 'Java SDK for Constant Contact API with RxJava'
            released  = new Date()
            vcsTag = ARTIFACT_VERSION
        }
    }
}
