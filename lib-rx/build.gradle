apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile project(':components')

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'io.reactivex:rxjava:1.2.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.btmatthews.hamcrest:hamcrest-matchers:1.0.1'
}

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

archivesBaseName = 'ctctapi'

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.constantcontact'
            artifactId 'java-sdk-rx'
            artifact sourceJar
            artifact javadocJar
            version '5.0.0'
        }
    }
}

bintray {
    user = project.hasProperty("bintray_user") ? bintray_user : "user"
    key = project.hasProperty("bintray_key") ? bintray_key : "key"

    publications = ['MyPublication']

    pkg {
        repo = 'maven'
        name = 'java-sdk-rx'
        userOrg = 'constantcontact'
        licenses = ['BSD 3-Clause']
        websiteUrl = 'https://github.com/constantcontact/java-sdk'
        issueTrackerUrl = 'https://github.com/constantcontact/java-sdk/issues'
        vcsUrl = 'https://github.com/constantcontact/java-sdk.git'
        labels = ['constant contact', 'constant', 'contact']

        version {
            name = '5.0.0'
            desc = 'Java SDK for Constant Contact API with RxJava'
            released  = new Date()
            vcsTag = '5.0.0'
        }
    }
}
